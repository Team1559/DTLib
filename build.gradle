plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
}

version = '0.1'

repositories {
  mavenCentral()

  maven { url 'https://frcmaven.wpi.edu/artifactory/release/' }

  maven { url 'https://maven.ctr-electronics.com/release/' }
  maven { url 'https://maven.revrobotics.com/' }
  maven { url 'https://maven.photonvision.org/repository/internal' }
}

dependencies {
  String wpilibVersion = '2023.4.3'
  implementation 'edu.wpi.first.wpilibj:wpilibj-java:' + wpilibVersion
  implementation 'edu.wpi.first.ntcore:ntcore-java:' + wpilibVersion
  implementation 'edu.wpi.first.hal:hal-java:' + wpilibVersion
  implementation 'edu.wpi.first.wpinet:wpinet-java:' + wpilibVersion
  implementation 'edu.wpi.first.wpiutil:wpiutil-java:' + wpilibVersion
  implementation 'edu.wpi.first.wpilibNewCommands:wpilibNewCommands-java:' + wpilibVersion

  String wpimathVersion = '2023.4.3'
  implementation 'edu.wpi.first.wpimath:wpimath-java:' + wpimathVersion

  String jacksonVersion = '2.12.4'
  implementation 'com.fasterxml.jackson.core:jackson-annotations:' + jacksonVersion

  String ejmlVersion = '0.42'
  implementation 'org.ejml:ejml-simple:' + ejmlVersion

  String navxVersion = '4.0.447'
  implementation 'com.kauailabs.navx.frc:navx-java:' + navxVersion

  String phoenix5Version = '5.30.4'
  implementation 'com.ctre.phoenix:api-java:' + phoenix5Version
  implementation 'com.ctre.phoenix:wpiapi-java:' + phoenix5Version

  String phoenix6Version = '23.2.2'
  implementation 'com.ctre.phoenix6:wpiapi-java:' + phoenix6Version

  String revlibVersion = '2023.1.3'
  implementation 'com.revrobotics.frc:REVLib-java:' + revlibVersion

  testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
}

test {
  useJUnitPlatform()
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

jar {
  dependsOn javadoc
  from sourceSets.main.allJava
  from javadoc.destinationDir
  archiveFileName = 'dtlib.jar'
}

task classesJar(type: Jar) {
  from sourceSets.main.output
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allJava
  archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
  dependsOn javadoc
  from javadoc.destinationDir
  archiveClassifier = 'javadoc'
}

artifacts {
  archives sourcesJar
  archives javadocJar
  archives classesJar
}

publishing {
  publications {
    mavenCentral(MavenPublication) {
      groupId = 'org.victorrobotics.dtlib'
      artifactId = 'dtlib-java'
      version = '0.1'

      artifact classesJar
      artifact sourcesJar
      artifact javadocJar

      pom {
        name = 'DTLib'
        description = 'An extension of WPILib developed by FRC Team 1559'
        url = 'http://github.com/Team1559/DTLib'
        inceptionYear = '2023'

        licenses {
          license {
            name = 'GNU General Public License, Version 3'
            url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
          }
        }

        organization {
          name = 'Victor Robotics - FRC Team 1559'
          url = 'https://victorrobotics.org'
        }

        developers {
          developer {
            name = 'Alexander Bhalla'
            email = 'xander.bhalla@gmail.com'
            url = 'https://github.com/MathNerd28'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/Team1559/DTLib.git'
          developerConnection = 'scm:git:ssh://github.com:Team1559/DTLib.git'
          url = 'https://github.com/Team1559/DTLib/tree/master'
        }
      }
    }
  }

  repositories {
    maven {
      name = 'OSSRH'
      url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
      credentials {
        username = System.getenv('DTLIB_PUBLISH_USERNAME')
        password = System.getenv('DTLIB_PUBLISH_PASSWORD')
      }
    }
  }
}

signing {
  useGpgCmd()
  sign publishing.publications.mavenCentral
}
