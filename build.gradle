plugins {
  id 'java-library'
  id 'maven-publish'
}

version = '0.1'

repositories {
  mavenCentral()

  maven { url 'https://frcmaven.wpi.edu/artifactory/release/' }
  maven { url 'https://maven.ctr-electronics.com/release/' }
  maven { url 'https://maven.revrobotics.com/' }
}

dependencies {
  // Current GradleRIO versions at https://github.com/wpilibsuite/GradleRIO/blob/main/src/main/java/edu/wpi/first/gradlerio/wpi/WPIVersionsExtension.java
  String wpilibVersion = '2024.1.1-beta-1'
  String jacksonVersion = '2.12.4'
  String ejmlVersion = '0.42'

  String navxVersion = '4.0.447'
  String phoenix5Version = '5.31.0'
  String phoenix6Version = '23.10.0-alpha-8'
  String revlibVersion = '2023.1.3'

  implementation 'edu.wpi.first.wpilibj:wpilibj-java:' + wpilibVersion
  implementation 'edu.wpi.first.ntcore:ntcore-java:' + wpilibVersion
  implementation 'edu.wpi.first.hal:hal-java:' + wpilibVersion
  implementation 'edu.wpi.first.wpinet:wpinet-java:' + wpilibVersion
  implementation 'edu.wpi.first.wpiutil:wpiutil-java:' + wpilibVersion
  implementation 'edu.wpi.first.wpilibNewCommands:wpilibNewCommands-java:' + wpilibVersion
  implementation 'edu.wpi.first.wpimath:wpimath-java:' + wpilibVersion
  implementation 'com.fasterxml.jackson.core:jackson-annotations:' + jacksonVersion
  implementation 'org.ejml:ejml-simple:' + ejmlVersion

  implementation 'com.kauailabs.navx.frc:navx-java:' + navxVersion
  implementation 'com.ctre.phoenix:api-java:' + phoenix5Version
  implementation 'com.ctre.phoenix:wpiapi-java:' + phoenix5Version
  implementation 'com.ctre.phoenix6:wpiapi-java:' + phoenix6Version
  implementation 'com.revrobotics.frc:REVLib-java:' + revlibVersion

  testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
}

test {
  useJUnitPlatform()
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17

  withSourcesJar()
}

jar {
  manifest {
    attributes (
      'Class-Path': configurations.compileClasspath.collect { it.name }.join(' ')
    )
  }
}

publishing {
  repositories {
    maven {
      name = 'GitHubPackages'
      url = uri('https://maven.pkg.github.com/Team1559/DTLib')
      credentials {
        username = project.findProperty('gpr.user') ?: 'Team1559'
        password = project.findProperty('gpr.key') ?: System.getenv('GHP_PUBLISH_TOKEN')
      }
    }
  }
  publications {
    gpr(MavenPublication) {
      groupId = 'org.victorrobotics.dtlib'
      artifactId = 'dtlib-java'
      version = '0.1'
      from(components.java)
    }
  }
}
